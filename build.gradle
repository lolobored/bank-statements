plugins {
  id 'java'
  id "io.franzbecker.gradle-lombok" version "5.0.0"
  id 'org.springframework.boot' version '2.7.4'
  id "net.researchgate.release" version "3.0.2"
  id "maven-publish"
}


group 'org.lolobored'
version project.version

repositories {
  mavenCentral()
}


sourceCompatibility = 11

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter', version: '2.7.4'
  implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.24'
  implementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '4.4.0'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
  implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
  implementation group: 'io.github.bonigarcia', name: 'webdrivermanager', version: '5.3.0'
  implementation group: 'com.opencsv', name: 'opencsv', version: '5.7.0'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.11.3'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.3'
}

configurations {
  [apiElements, runtimeElements].each {
    it.outgoing.artifacts.removeIf {
      it.buildDependencies.getDependencies(null).contains(jar)
    }
    it.outgoing.artifact(bootJar)
  }
}

release {
  afterReleaseBuild.dependsOn publish
}
publishing {
  repositories {
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/lolobored/bank-statements")
      credentials {
        username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
        password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
      }
    }
  }
  publications {
    gpr(MavenPublication) {
      from(components.java)
    }
  }
}


