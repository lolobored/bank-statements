plugins {
  id 'java'
  id "io.franzbecker.gradle-lombok" version "3.2.0"
  id 'org.springframework.boot' version '2.2.1.RELEASE'
  id "net.researchgate.release" version "2.8.1"
  id "maven-publish"
}


group 'org.lolobored'
version project.version

repositories {
  mavenCentral()
}


sourceCompatibility = 11

dependencies {
  compile group: 'org.springframework.boot', name: 'spring-boot-starter', version: '2.4.0'
  compile group: 'org.projectlombok', name: 'lombok', version: '1.18.16'
  compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59'
  compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
  compile group: 'commons-io', name: 'commons-io', version: '2.8.0'
  compile group: 'io.github.bonigarcia', name: 'webdrivermanager', version: '4.2.2'
  compile group: 'com.opencsv', name: 'opencsv', version: '5.3'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.11.3'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.3'
}

configurations {
  [apiElements, runtimeElements].each {
    it.outgoing.artifacts.removeIf {
      it.buildDependencies.getDependencies(null).contains(jar)
    }
    it.outgoing.artifact(bootJar)
  }
}

release {
  afterReleaseBuild.dependsOn publish
}
publishing {
  repositories {
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/lolobored/bank-statements")
      credentials {
        username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
        password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
      }
    }
  }
  publications {
    gpr(MavenPublication) {
      from(components.java)
    }
  }
}


